plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.10.1'
}

group = 'de.clashsoft'
version = 'git describe --tags'.execute().text[1..-2] // strip v and \n
description = 'Integrate Angular frontends into your Gradle build.'

pluginBundle {
    website = 'https://github.com/Clashsoft/Angular-Gradle'
    vcsUrl = 'https://github.com/Clashsoft/Angular-Gradle'
    description = project.description
    tags = [ 'angular', 'frontend', 'node' ]

    plugins {
        angularGradle {
            displayName = 'Angular Gradle'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

gradlePlugin {
    plugins {
        angularGradle {
            id = 'de.clashsoft.angular-gradle'
            implementationClass = 'de.clashsoft.gradle.angular.AngularGradlePlugin'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

tasks.register('functionalTest', Test) {
    it.testClassesDirs = sourceSets.functionalTest.output.classesDirs
    it.classpath = sourceSets.functionalTest.runtimeClasspath
}

check.dependsOn 'functionalTest'
